import groovy.json.JsonSlurper

plugins {
    id "java"
    id "org.openjfx.javafxplugin" version "0.1.0"
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
    flatDir { dirs mcreator_path + '/lib' }
}

group  = 'net.zsemper.jeii'
version = new JsonSlurper().parse(file('src/main/resources/plugin.json'))['info']['version']

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    implementation.extendsFrom export
}

dependencies {
    implementation project(':MCreator')

    project(':MCreator').afterEvaluate(() ->
            project(":MCreator").configurations.named("implementation").get().dependencies.each {
                //noinspection ForeignDelegate
                implementation it
            }
    )

    implementation 'org.jetbrains:annotations:20.1.0'

    // Uncomment the following two lines if you need to use JavaFX in your plugin
    // implementation group: 'org.openjfx', name: 'javafx-web', version: '21.0.7'
    // implementation group: 'org.openjfx', name: 'javafx-swing', version: '21.0.7'

    // Use "export" configuration for dependencies that should be included in the plugin (if MCreator does not provide them)
    // export group: 'org.example', name: 'example-library', version: '1.2.3'
}

javafx {
    version = "21.0.7"
    modules = [ "javafx.web", "javafx.swing" ]
    configuration = "compileOnly"
}

tasks.jar {
    archiveFileName.set("jei_integration_v1.5.0_25.2.zip")

    from {
        configurations.export.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))
    environment("MCREATOR_PLUGINS_DEV", "")

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath

    main = 'net.mcreator.Launcher'

    workingDir = mcreator_path
}

subprojects { subproject ->
    subproject.plugins.withType(JavaPlugin) {
        test {
            dependsOn rootProject.jar

            environment("MCREATOR_PLUGINS_FOLDER", new File(rootProject.projectDir, "/build/libs"))
            environment("MCREATOR_PLUGINS_DEV", "")

            workingDir = mcreator_path
        }
    }
}